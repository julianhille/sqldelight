buildscript {
  dependencies {
    classpath("com.squareup.okhttp3:okhttp") {
      version {
        strictly '4.4.1'
      }
    }
  }
}

plugins {
  alias(deps.plugins.grammarKitComposer) apply false
  alias(deps.plugins.kotlin.multiplatform) apply false
  alias(deps.plugins.kotlin.jvm) apply false
  alias(deps.plugins.kotlin.android) apply false
  alias(deps.plugins.kotlin.js) apply false
  alias(deps.plugins.ksp) apply false
  alias(deps.plugins.android.application) apply false
  alias(deps.plugins.android.library) apply false
  alias(deps.plugins.publish) apply false
  alias(deps.plugins.dokka) apply false
  alias(deps.plugins.spotless)
}

spotless {
  kotlin {
    target "**/*.kt"
    targetExclude "**/gen/**/*.*", "**/generated/**/*.*", "sqldelight-compiler/integration-tests/src/test/kotlin/com/example/**/*.*", "sqldelight-compiler/src/test/migration-interface-fixtures/**/*.*"
    ktlint(deps.versions.ktlint.get()).editorConfigOverride([
      "indent_size": "2",
       "disabled_rules": "package-name",
       "ij_kotlin_allow_trailing_comma": "true",
       "ij_kotlin_allow_trailing_comma_on_call_site": "true",
    ])
    trimTrailingWhitespace()
    endWithNewline()
  }
}

allprojects {
  repositories {
    mavenCentral()
    google()
    maven { url 'https://www.jetbrains.com/intellij-repository/releases' }
    maven { url "https://cache-redirector.jetbrains.com/intellij-dependencies" }
  }

  tasks.withType(Test).configureEach {
    testLogging {
      events = ["failed", "skipped", "passed"]
      exceptionFormat "full"
    }
  }

  tasks.withType(JavaCompile) {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
  }

  tasks.withType(org.jetbrains.grammarkit.tasks.GenerateParserTask) {
    doFirst {
      // https://github.com/JetBrains/gradle-grammar-kit-plugin/pull/78.
      logging.captureStandardOutput(LogLevel.INFO)
    }
  }

  // https://youtrack.jetbrains.com/issue/KT-46466
  tasks.withType(org.gradle.plugins.signing.Sign) { signingTask ->
    tasks.withType(org.gradle.api.publish.maven.tasks.AbstractPublishToMaven) { publishTask ->
      publishTask.dependsOn(signingTask)
    }
  }

  configurations.all {
    exclude group: 'com.jetbrains.rd'
    exclude group: 'com.github.jetbrains', module: 'jetCheck'
    exclude group: 'org.roaringbitmap'
  }

  group = GROUP
  version = VERSION_NAME
}
